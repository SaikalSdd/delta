struct Tape
{
    List<int> tape;
    int pos;

    init()
    {
        tape = List<int>([0]);
        pos = 0;
    }

    int value()
    {
        return tape[pos];
    }

    void increment()
    {
        tape[pos]++;
    }

    void decrement()
    {
        tape[pos]--;
    }

    void forward()
    {
        pos++;

        if (pos >= tape.size())
            tape.push(0);
    }

    void backward()
    {
        if (pos > 0)
            pos--;
    }
}

struct Program
{
    StringBuffer code;
    Map<int, int> bracketMap;

    init(string text)
    {
        this.code = StringBuffer();
        this.bracketMap = Map<int, int>();

        var leftStack = List<int>();
        var pc = 0;

        for (var ch in text)
        {
            if (!"+-><[].,".contains(ch))
                continue;

            switch (ch)
            {
                case '[':
                    leftStack.push(pc);

                case ']':
                    if (!leftStack.empty())
                    {
                        var left = leftStack.pop();
                        var right = pc;
                        bracketMap.set(left, right);
                        bracketMap.set(right, left);
                    }
            }

            pc++;
            code.push(ch);
        }
    }

    void run()
    {
        var tape = Tape();
        var pc = 0;
        var size = code.size();

        while (pc < size)
        {
            switch (code[pc])
            {
                case '+':
                    tape.increment();

                case '-':
                    tape.decrement();

                case '>':
                    tape.forward();

                case '<':
                    tape.backward();

                case '[':
                    if (tape.value() == 0)
                        pc = *bracketMap[pc]!;

                case ']':
                    if (tape.value() != 0)
                        pc = *bracketMap[pc]!;

                case '.':
                    printf("%c", char(tape.value()));
            }

            pc++;
        }
    }
}

void main()
{
    var file = FileStream("./inputs/mandel.b");
    var text = StringBuffer();

    for (var byte in file.bytes())
        text.push(char(byte));

    Program(string(text)).run();
}
