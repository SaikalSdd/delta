// RUN: check_exit_status 0 %delta run -Werror %s

void main() {
    testInsert();
    testRemoval();
    testDuplicates();
    testGetter();
    testResize();
    testSize();
    testIterator();
    testEmptyMapIterator();
    testUnitMapIterator();
}

void testInsert() {
    var map = Map<StringRef, int>();

    map.insert("ok", 1);

    assert(map.contains("ok"));
    assert(!map.contains("lol"));
}

void testSize() {
    var map = Map<StringRef, StringRef>();

    assert(map.size() == 0);
    map.insert("best", "ok");
    assert(map.size() == 1);
}

void testGetter() {
    var map = Map<StringRef, int>();

    map.insert("ok", 42);
    assert(map["ok"]! == 42); // FIXME: Fix compilation without the unwrap.
}

void testResize() {
    var map = Map<String, int>();

    for (var i in 0..255) {
        var lol = String();

        for (var j in 0..(i+1)) {
            lol.push('a');
        }

        map.insert(lol, i);
    }

    assert(map.size() == 255);
}


void testRemoval() {
    var map = Map<StringRef, int>();
    assert(map.size() == 0);

    map.insert("ok", 2);

    assert(map.contains("ok"));
    assert(map.size() == 1);

    map.remove("ok");
    assert(!map.contains("ok"));
    assert(map.size() == 0);
}

void testDuplicates() {
    var map = Map<StringRef, int>();

    map.insert("ok", 1);
    map.insert("ok", 2);
    assert(map.contains("ok"));

    assert(map["ok"]! == 1); // FIXME: Fix compilation without the unwrap.

    map.remove("ok");
    assert(!map.contains("ok"));
    assert(map["ok"] == null);
}

void testIterator() {
    var map = Map<StringRef, StringRef>();

    map.insert("a", "a");
    map.insert("ccc", "ccc");
    map.insert("bb", "bb");
    map.insert("a", "a");
    map.insert("ccc", "ccc");
    map.insert("ccc", "ccc");

    var a = false;
    var bb = false;
    var ccc = false;

    for (var e in map) {
        if (e.key == "a") {
            assert(!a);
            a = true;
        } else if (e.key == "bb") {
            assert(!bb);
            bb = true;
        } else if (e.key == "ccc") {
            assert(!ccc);
            ccc = true;
        } else {
            assert(false);
        }
    }

    assert(a);
    assert(bb);
    assert(ccc);
}

void testEmptyMapIterator() {
    var map = Map<StringRef, StringRef>();
    map.insert("foo", "foo");
    map.insert("foo", "foo");
    map.remove("foo");

    for (var e in map) {
        assert(false);
    }
}

void testUnitMapIterator() {
    var map = Map<StringRef, StringRef>();
    map.insert("foo", "foo");
    map.insert("foo", "foo");
    var count = 0;

    for (var e in map) {
        count++;
        assert(e.key == "foo");
    }

    assert(count == 1);
}
