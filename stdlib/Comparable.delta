/// A type whose instances have an inherent sort-order, i.e. can be compared with the `<`, `<=`,
/// `>`, `>=` operators.
interface Comparable {

    /// Returns `Ordering.Less` if `this` is less than `other`, `Ordering.Equal` if `this` is equal
    /// to `other`, or `Ordering.Greater` if `this` is greater than `other`.
    func compare(other: This*) -> Ordering;
}

enum Ordering {
    case Less;
    case Equal;
    case Greater;
}

func == <T: Comparable>(a: T*, b: T*) -> bool {
    return a.compare(b) == Ordering.Equal;
}

func != <T: Comparable>(a: T*, b: T*) -> bool {
    return a.compare(b) != Ordering.Equal;
}

func < <T: Comparable>(a: T*, b: T*) -> bool {
    return a.compare(b) == Ordering.Less;
}

func > <T: Comparable>(a: T*, b: T*) -> bool {
    return a.compare(b) == Ordering.Greater;
}

func >= <T: Comparable>(a: T*, b: T*) -> bool {
    return a.compare(b) != Ordering.Less;
}

func <= <T: Comparable>(a: T*, b: T*) -> bool {
    return a.compare(b) != Ordering.Greater;
}