struct Array<Element> {
    var bufferPointer: mutable Element*;
    var size: uint;
    var capacity: uint;

    /// Initializes an empty array.
    init() {
        bufferPointer = undefined;
        size = 0;
        capacity = 0;
    }

    /// Initializes an empty array with pre-allocated capacity.
    init(capacity: uint) {
        init();
        reserve(capacity);
    }

    /// Initializes an array containing the elements of the given array.
    init(elements: Element[]*) {
        init(capacity: elements.size());

        // TODO: Change this to a for-each loop when arrays can be iterated.
        for (let index in 0..elements.size()) {
            push(elements[index]);
        }
    }

    deinit() {
        if (capacity != 0) {
            deallocate(bufferPointer);
        }
    }

    /// Returns the number of elements in the array.
    def size(): uint {
        return size;
    }

    /// Returns true if the array has no elements, otherwise false
    def isEmpty(): bool {
        return size == 0;
    }

    /// Returns the number of elements the array can store without allocating more memory.
    def capacity(): uint {
        return capacity;
    }

    /// Returns the element at the given index.
    def [](index: uint): Element* {
        if (index >= size) {
            indexOutOfBounds(index);
        }

        return bufferPointer + index;
    }

    /// Returns the element at the given index.
    mutating def [](index: uint): mutable Element* {
        if (index >= size) {
            indexOutOfBounds(index);
        }

        return bufferPointer + index;
    }

    def first(): Element* {
        if (size == 0) {
            emptyArrayFirst();
        }

        return bufferPointer;
    }

    def last(): Element* {
        if (size == 0) {
            emptyArrayLast();
        }

        return bufferPointer + size;
    }

    def data(): Element* {
        return bufferPointer;
    }

    mutating def data(): mutable Element* {
        return bufferPointer;
    }

    /// Adds the given element to the end of the array.
    mutating def push(newElement: Element) {
        if (size == capacity) {
            grow();
        }

        (bufferPointer + size).init(newElement);
        size++;
    }

    /// Ensures that the capacity is large enough to store the given number of elements.
    mutating def reserve(minimumCapacity: uint) {
        if (minimumCapacity > capacity) {
            let newBufferPointer = allocateArray<Element>(size: minimumCapacity);

            for (let index in 0..size) {
                let source = bufferPointer + index;
                let target = newBufferPointer + index;
                target.init(*source);
            }

            if (capacity != 0) {
                deallocate(bufferPointer);
            }

            bufferPointer = newBufferPointer;
            capacity = minimumCapacity;
        }
    }

    /// Removes the first element from the array.
    /// Other elements are moved towards the beginning of the array by one index.
    mutating def removeFirst() {
        if (size == 0) {
            emptyArrayRemoveFirst();
        }

        unsafeRemoveAt(0);
    }

    /// Removes the last element from the array.
    mutating def removeLast() {
        if (size == 0) {
            emptyArrayRemoveLast();
        }

        size--;
        (bufferPointer + size).deinit();
    }

    /// Removes the element at the given index from the array.
    /// Elements following the removed element are moved towards the beginning of the array by one index.
    mutating def removeAt(index: uint) {
        if (index >= size) {
            indexOutOfBounds(index);
        }

        unsafeRemoveAt(index);
    }

    mutating private def unsafeRemoveAt(index: uint) {
        (bufferPointer + index).deinit();

        for (let i in (index + 1)..size) {
            let source = bufferPointer + i;
            let target = bufferPointer + (i - 1);
            target.init(*source);
        }

        size--;
    }

    def iterator(): ArrayIterator<Element> {
        // TODO: When implicit initializers have been implemented, remove the ArrayRef() call below.
        return ArrayIterator(ArrayRef(this));
    }

    mutating def iterator(): MutableArrayIterator<Element> {
        return MutableArrayIterator(this);
    }

    def enumerate(): EnumeratedIterator<Element> {
        return EnumeratedIterator(iterator());
    }

    def map<Input, Output>(transform: (Input) -> Output): Array<Output> {
        var output = Array<Output>(capacity: this.size);

        for (let element in this) {
            output.push(transform(element));
        }

        return output;
    }

    def filter<Input>(include: (Input) -> bool): Array<Element> {
        var output = Array<Element>();

        for (let element in this) {
            if (include(element)) {
                output.push(*element);
            }
        }

        return output;
    }

    mutating private def grow() {
        if (capacity == 0) {
            reserve(1);
        } else {
            reserve(capacity * 2);
        }
    }

    private def indexOutOfBounds(index: uint) {
        printf("Array index %u is out of bounds, size is %u\n", index, size());
        fatalError();
    }

    private def emptyArrayFirst() {
        printf("Called first() on empty Array\n");
        fatalError();
    }

    private def emptyArrayLast() {
        printf("Called last() on empty Array\n");
        fatalError();
    }

    private def emptyArrayRemoveFirst() {
        printf("Called removeFirst() on empty Array\n");
        fatalError();
    }

    private def emptyArrayRemoveLast() {
        printf("Called removeLast() on empty Array\n");
        fatalError();
    }
}

def ==<T>(a: ArrayRef<T>, b: ArrayRef<T>): bool {
    if (a.size() != b.size()) {
        return false;
    }

    for (let i in 0..a.size()) {
        if (*a[i] != *b[i]) {
            return false;
        }
    }

    return true;
}
