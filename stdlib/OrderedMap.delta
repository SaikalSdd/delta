/// An ordered key-value container that supports fast insertion, deletion and lookup
class OrderedMap<Key, Value> {

    var root: mutable AVLnode<Key, Value>*?;
    var size: uint;

    init() {
        root = null;
        size = 0;
    }

    deinit() {
        if (root != null) {
            root!.deallocate();
        }
    }

    /// Returns the size of the Map
    func size() -> uint {
        return size;
    }

    /// Returns true if the Map is empty, otherwise false
    func isEmpty() -> bool {
        return size == 0;
    }

    mutating func _rebalance(node: mutable AVLnode<Key, Value>*) {
        var n = node;
        _setBalance(n);

        if (n.balance == -2) {
            if (_height(n.left!.left) >= _height(n.left!.right)) {
                n = _rotateRight(n);
            } else {
                n = _rotateLeftThenRight(n);
            }
        } else if (n.balance == 2) {
            if (_height(n.right!.right) >= _height(n.right!.left)) {
                n = _rotateLeft(n);
            } else {
                n = _rotateRightThenLeft(n);
            }
        }

        if (n.parent != null) {
            _rebalance(n.parent!);
        } else {
            root = n;
        }
    }

    func _rotateLeft(node: mutable AVLnode<Key, Value>*) -> mutable AVLnode<Key, Value>* {
        var a = node;
        var b = a.right!;
        b.parent = a.parent;
        a.right = b.left;

        if (a.right != null) {
            a.right!.parent = a;
        }

        b.left = a;
        a.parent = b;

        if (b.parent != null) {
            if (addressof(b.parent!.right) == addressof(a)) {
                b.parent!.right = b;
            } else {
                b.parent!.left = b;
            }
        }

        _setBalance(a);
        _setBalance(b);
        return b;
    }

    func _rotateRight(node: mutable AVLnode<Key, Value>*) -> mutable AVLnode<Key, Value>* {
        var a = node;
        var b = a.left!;
        b.parent = a.parent;
        a.left = b.right;

        if (a.left != null) {
            a.left!.parent = a;
        }

        b.right = a;
        a.parent = b;

        if (b.parent != null) {
            if (addressof(b.parent!.right) == addressof(a)) {
                b.parent!.right = b;
            } else {
                b.parent!.left = b;
            }
        }

        _setBalance(a);
        _setBalance(b);
        return b;
    }

    func _rotateLeftThenRight(n: mutable AVLnode<Key, Value>*) -> mutable AVLnode<Key, Value>* {
        n.left = _rotateLeft(n.left!);
        return _rotateRight(n);
    }

    func _rotateRightThenLeft(n: mutable AVLnode<Key, Value>*) -> mutable AVLnode<Key, Value>* {
        n.right = _rotateRight(n.right!);
        return _rotateLeft(n);
    }

    func _height(n: AVLnode<Key, Value>*?) -> int {
        if (n == null) {
            return -1;
        }
        return 1 + *max(_height(n!.left), _height(n!.right));
    }

    func _setBalance(n: mutable AVLnode<Key, Value>*) {
        n.balance = _height(n.right) - _height(n.left);
    }

    /// Checks if the given element is in the Map. Returns true if it is, otherwise false
    func contains(key: Key*) -> bool {
        if (root == null) {
            return false;
        }
        var n = root!;
        while (true) {
            if (n.key == *key) {
                return true;
            }

            let goLeft = n.key > *key;
            let newNode = goLeft ? n.left : n.right;

            if (newNode == null) {
                return false;
            }
            n = newNode!;
        }
        return false;
    }

    /// Returns the value of the given key, if it exists in the Map. Otherwise, returns null.
    func [](key: Key*) -> Value*? {
        let found = find(key);
        if (found != null) {
            return found!.value;
        }
        return null;
    }

    /// Inserts a key-value pair into the Map.
    mutating func insert(key: Key, value: Value) -> bool {
        if (root == null) {
            root = allocate(AVLnode<Key, Value>(key, null, value));
        } else {
            var n = root!;
            var parent: mutable AVLnode<Key, Value>*? = null;

            while (true) {
                if (n.key == key) {
                    return false;
                }

                parent = n;

                let goLeft = n.key > key;
                let newNode = goLeft ? n.left : n.right;

                if (newNode == null) {
                    if (goLeft) {
                        parent!.left = allocate(AVLnode<Key, Value>(key, parent, value));
                    } else {
                        parent!.right = allocate(AVLnode<Key, Value>(key, parent, value));
                    }

                    _rebalance(parent!);
                    break;
                }
                n = newNode!;
            }
        }

        size++;
        return true;
    }

    /// Removes a key and it's associated value from the Map
    mutating func remove(key: Key*) {
        if (root == null) {
            return;
        }

        var n = root!;
        var parent = root;
        var delNode: mutable AVLnode<Key, Value>*? = null;
        var child = root;

        while (child != null) {
            parent = n;
            n = child!;
            child = (*key >= n.key) ? n.right : n.left;
            if (*key == n.key) {
                delNode = n;
            }
        }

        if (delNode != null) {
            delNode!.key = n.key;
            delNode!.value = n.value;

            child = (n.left != null) ? n.left : n.right;

            if (root!.key == *key) {
                root = child;
            } else {
                if (addressof(parent!.left) == addressof(n)) {
                    parent!.left = child;
                } else {
                    parent!.right = child;
                }

                _rebalance(parent!);
            }
            n.deinit();
            deallocate(n);
        }
        size--;
    }

    /// Returns the node corresponding to `key`, or null if it doesn't exist in the Map
    func find(key: Key*) -> AVLnode<Key, Value>*? {
        if (root == null) {
            return null;
        }

        var n = root;
        while (true) {
            if (n!.key == *key) {
                return n!;
            }

            let goLeft = n!.key > *key;
            n = goLeft ? n!.left : n!.right;

            if (n == null) {
                return null;
            }
        }
        return null;
    }

    /// Returns the key immediately smaller than the one given,
    /// or null, if `key` is the smallest in the Map.
    func lowerKey(key: Key*) -> Key*? {
        let found = find(key);
        if (found == null) {
            return null;
        }
        let lower = lowerNode(found!);

        if (lower == null) {
            return null;
        }
        return lower!.key;
    }

    func _maxInSubtree(node: AVLnode<Key, Value>*) -> AVLnode<Key, Value>* {
        var n = node;
        while (n.right != null) {
            n = n.right!;
        }
        return n;
    }

    func _minInSubtree(node: mutable AVLnode<Key, Value>*) -> mutable AVLnode<Key, Value>* {
        var n = node;
        while (n.left != null) {
            n = n.left!;
        }
        return n;
    }

    /// Returns the node that contains the key immediately smaller than the key of the given node,
    /// or null, if the key of `node` is the smallest value.
    func lowerNode(node: AVLnode<Key, Value>*) -> AVLnode<Key, Value>*? {
        if (node.left != null) {
            return _maxInSubtree(node.left!);
        }

        var n = node;
        while (n.parent != null) {
            if (addressof(n) == addressof(n.parent!.right)) {
                return n.parent;
            }
            n = n.parent!;
        }
        return null;
    }

    /// Returns the key immediately higher than the one given,
    /// or null, if `key` is the largest in the Map.
    func higherKey(key: Key*) -> Key*? {
        let found = find(key);
        if (found == null) {
            return null;
        }

        let higher = higherNode(found!);

        if (higher == null) {
            return null;
        }
        return higher!.key;
    }

    /// Returns the node that contains the key immediately larger than the key of the given node,
    /// or null, if the key of `node` is the largest value.
    func higherNode(node: AVLnode<Key, Value>*) -> mutable AVLnode<Key, Value>*? {
        if (node.right != null) {
            return _minInSubtree(node.right!);
        }

        var n = node;
        while (n.parent != null) {
            if (addressof(n) == addressof(n.parent!.left)) {
                return n.parent;
            }
            n = n.parent!;
        }
        return null;
    }


    /// Returns the smallest element in the Map, or null if the Map is empty.
    func first() -> Key*? {
        if (root == null) {
            return null;
        }
        return _minInSubtree(root!).key;
    }

    /// Returns the node with the smallest key in the Map, or null if the Map is empty.
    func firstNode() -> mutable AVLnode<Key, Value>*? {
        if (root == null) {
            return null;
        }
        return _minInSubtree(root!);
    }

    /// Returns the largest element in the Map, or null if the map is empty.
    func last() -> Key*? {
        if (root == null) {
            return null;
        }
        return _maxInSubtree(root!).key;
    }

    /// Returns the node with the largest key in the Map, or null if the Map is empty.
    func lastNode() -> AVLnode<Key, Value>*? {
        if (root == null) {
            return null;
        }
        return _maxInSubtree(root!);
    }


    /// Iterate over the map, in order
    func iterator() -> OrderedMapIterator<Key, Value> {
        return OrderedMapIterator(this);
    }

}

/// One node in the AVL tree
class AVLnode<Key, Value> {
    var key: Key;
    var value: Value;
    var balance: int;
    var parent: mutable AVLnode<Key, Value>*?;
    var left: mutable AVLnode<Key, Value>*?;
    var right: mutable AVLnode<Key, Value>*?;

    init(key: Key, parent: mutable AVLnode<Key, Value>*?, value: Value) {
        this.key = key;
        this.value = value;
        this.parent = parent;
        this.balance = 0;
        this.left = null;
        this.right = null;
    }

    mutating func deallocate() {
        deallocate(this);

        if (left != null) {
            left!.deallocate();
        }
        if (right != null) {
            right!.deallocate();
        }
    }
}
