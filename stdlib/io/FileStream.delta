/// A Stream that can both read and write to files
class FileStream: InputStream, OutputStream {

    var file: void*?;

    /// Initiates a stream that can both read and write to file
    init(filename: StringRef) {
        file = fopen(String(filename).cString(), "a+");

        if (file != null) {
            rewind(file);
        }
    }

    /// TODO when we have default params, switch to that
    init(filename: StringRef, destroyContents: bool) {
        if (destroyContents) {
            file = fopen(String(filename).cString(), "w+");
        } else {
            init(filename);
        }
    }

    // FIXME: Remove readOnly parameter, FileStream can both read and write.
    init(filename: StringRef, readOnly: bool) {
        file = fopen(String(filename).cString(), "r");
    }

    // FIXME: Remove readOnly parameter, FileStream can both read and write.
    init(fileDescriptor: int, readOnly: bool) {
        if (readOnly) {
            file = fdopen(fileDescriptor, "r");
        } else {
            file = fdopen(fileDescriptor, "w");
        }
    }

    deinit() {
        if (file != null) {
            fclose(file);
        }
    }

    mutating func close() {
        if (file != null) {
            fclose(file);
            file = null;
        }
    }

    /// Returns true if the file is open for reading or writing
    func isOpen() -> bool {
        return file != null;
    }

    /// Returns an iterator that can be used to iterate over lines in the file
    func lines() -> LineIterator<FileStream> {
        return LineIterator(this);
    }

    /// Returns an iterator that can be used to iterate over each byte in the file
    func bytes() -> ByteIterator<FileStream> {
        return ByteIterator(this);
    }

    /// Reads one byte from the file
    func readByte() -> int {
        return fgetc(file!);
    }

    /// Reads one line from the file, separated by \n
    func readLine() -> String {
        var ret = String();
        for (let i in bytes()) {
            if (i == '\n') {
                break;
            }
            ret.push(char(i));
        }
        return ret;
    }

    /// Returns true if we have reached the end of the file
    func isEof() -> bool {
        let ret = fgetc(file!);
        if (ret < 0) {
            return true;
        }
        ungetc(ret, file!);
        return false;
    }

    /// Writes the given string into the file. Returns true on success, false on failure
    mutating func write(string: StringRef) -> bool {
        for (let c in string) {
            let ret = fputc(int(c), file!);
            if (ret <= 0) {
                return false;
            }
        }
        return true;
    }
}
