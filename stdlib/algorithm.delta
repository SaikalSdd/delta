/// Public members

/// Gives the maximum of the two elements
func max<T>(a: T*, b: T*) -> T* {
    if (*a >= *b) {
        return a;
    }
    return b;
}

/// Gives the minimum of the two elements
func min<T>(a: T*, b: T*) -> T* {
    if (*a < *b) {
        return a;
    }
    return b;
}

/// Swaps two items in place
func swap<E>(a: mutable E*, b: mutable E*) {
    let t = *a;
    *a = *b;
    *b = t;
}

/// Sorts an array. Uses insertion sort if the array is small enough, and quickSort otherwise.
func sort<E: Comparable>(array: mutable Array<E>*) {
    if (array.size() <= 32) {
        insertionSort(array);
    } else {
        quickSort(array, 0, array.size() - 1);
    }
}

/// Private members

private func partition<E: Comparable>(arr: mutable Array<E>*, low: uint, high: uint) -> uint {
    var pivot = arr[high];
    var i = (low - 1);

    for (let j in low..high) {
        if (arr[j] < pivot) {
            i++;
            swap<E>(arr[i], arr[j]);
        }
    }
    if (arr[high] < arr[i+1]) {
        swap<E>(arr[i + 1], arr[high]);
    }
    return (i + 1);
}

/// Quicksorts the given array, in place. Not stable.
private func quickSort<E: Comparable>(arr: mutable Array<E>*, low: uint, high: uint) {
    if (low < high) {
        let index = partition<E>(arr, low, high);

        if (low < index) {
            quickSort(arr, low, index - 1);
        }
        if (index < high) {
            quickSort(arr, index + 1, high);
        }
    }
}

/// Insertion sorts the given array, in place. Good for small arrays
private func insertionSort<E: Comparable>(arr: mutable Array<E>*) {
    var i: uint = 1;
    while (i < arr.size()) {
        var j: uint = i;
        while (j > 0 && arr[j-1] > arr[j]) {
            swap(arr[j], arr[j-1]);
            j--;
        }
        i++;
    }
}
