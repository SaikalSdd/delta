struct StringBuffer: Comparable, Hashable, OutputStream, Printable {
    List<char> characters;

    /// Initializes an empty string.
    init() {
        characters = List<char>();
        characters.push('\0');
    }

    init(string s) {
        characters = List<char>(capacity: s.size() + 1);

        for (var ch in s) {
            characters.push(ch);
        }

        characters.push('\0');
    }

    /// Initializes a string with the characters from a character array of known length.
    init(char* pointer, int length) {
        init(string(pointer, length));
    }

    /// Initializes a string with the contents of a null-terminated C-style string.
    ///
    /// __Note:__ This initializer has a time complexity of O(n) because it has to calculate the
    /// length of the argument string. If you know the length beforehand, consider using
    /// `StringBuffer.init(char* pointer, uint length)` instead.
    ///
    init(char* cString) {
        init(string(cString, int(strlen(cString))));
    }

    int size() {
        return characters.size() - 1;
    }

    /// Returns the character at the given index.
    char operator[](int index) {
        return characters[index];
    }

    /// Returns the string as a C-style, i.e. null-terminated, string.
    /// Modifying `this` after calling this function invalidates the returned pointer.
    char* cString() {
        return data();
    }

    /// Returns a pointer to the first character in the string.
    char[?]* data() {
        return characters.data();
    }

    bool empty() {
        return size() == 0;
    }

    void push(char character) {
        characters[size()] = character;
        characters.push('\0');
    }

    bool write(string s) {
        // TODO: Optimize
        for (var ch in s) {
            push(ch);
        }
        return true;
    }

    /// Removes the first character from the string.
    /// Other characters are moved towards the beginning of the string by one index.
    void removeFirst() {
        characters.removeFirst();
    }

    /// Removes the last character from the string.
    void removeLast() {
        characters.removeAt(characters.size() - 2);
    }

    /// Supports using strings with sets and dicts
    uint64 hash() {
        return string(this).hash();
    }

    StringIterator iterator() {
        // TODO: When implicit initializers have been implemented, remove the string() call below.
        return StringIterator(string(this));
    }

    bool contains(char c) {
        return find(c, 0) != -1;
    }

    /// Returns the index of the given character, or -1 if it's not found
    int find(char c) {
        return find(c, 0);
    }

    /// Returns the index of the given character, or -1 if it's not found.
    /// Starts from start
    int find(char c, int start) {
        for (var i in start..size()) {
            if (characters[i] == c) {
                return i;
            }
        }
        return -1;
    }

    // Returns the substring of the string in the given range, [inclusive, exclusive]
    string substr(Range<int> range) {
        if (range.end > size()) {
            indexOutOfBounds(range.end);
        }
        return string(characters[range.start], range.size());
    }

    // Splits the string by the given delimiter
    List<string> split(char delim) {
        var tokens = List<string>();

        var prev = 0;
        var pos = 0;

        while (true) {
            pos = find(delim, prev);

            if (pos == -1) {
                pos = size();
            }

            var token = substr(prev..pos);
            tokens.push(token);

            prev = pos + 1;

            if (pos > size() || prev > size()) {
                break;
            }
        }
        return tokens;
    }

    // Splits the string by whitespace
    List<string> split() {
        return split(' ');
    }

    void print(StringBuffer* stream) {
        stream.write(string(this));
    }

    Ordering compare(StringBuffer* other) {
        return string(this).compare(string(other));
    }

    private void indexOutOfBounds(int index) {
        printf("Range index %d is out of bounds, size is %d\n", index, size());
        fatalError();
    }
}

StringBuffer operator+(string a, string b) {
    var result = StringBuffer();

    for (var ch in a) {
        result.push(ch);
    }

    for (var ch in b) {
        result.push(ch);
    }

    return result;
}

StringBuffer operator+(StringBuffer a, string b) {
    var result = a;

    for (var ch in b) {
        result.push(ch);
    }

    return result;
}

bool operator==(StringBuffer* a, StringBuffer* b) {
    return string(a) == string(b);
}

bool operator==(StringBuffer* a, string b) {
    return string(a) == b;
}

bool operator==(string a, StringBuffer* b) {
    return a == string(b);
}
