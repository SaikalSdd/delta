struct Array<Element> {
    mutable Element[?]* buffer;
    int size;
    int capacity;

    /// Initializes an empty array.
    init() {
        buffer = undefined;
        size = 0;
        capacity = 0;
    }

    /// Initializes an empty array with pre-allocated capacity.
    init(int capacity) {
        init();
        reserve(capacity);
    }

    /// Initializes an array containing the elements of the given array.
    init(Element[]* elements) {
        init(capacity: elements.size());

        // TODO: Change this to a for-each loop when arrays can be iterated.
        for (var index in 0..elements.size()) {
            push(elements[index]);
        }
    }

    deinit() {
        if (capacity != 0) {
            // FIXME: Call deinitializers of remaining elements.
            deallocate(buffer);
        }
    }

    /// Returns the number of elements in the array.
    int size() {
        return size;
    }

    /// Returns true if the array has no elements, otherwise false
    bool empty() {
        return size == 0;
    }

    /// Returns the number of elements the array can store without allocating more memory.
    int capacity() {
        return capacity;
    }

    /// Returns the element at the given index.
    Element* operator[](int index) {
        if (index >= size) {
            indexOutOfBounds(index);
        }

        return buffer[index];
    }

    /// Returns the element at the given index.
    mutating mutable Element* operator[](int index) {
        if (index >= size) {
            indexOutOfBounds(index);
        }

        return buffer[index];
    }

    Element* first() {
        if (size == 0) {
            emptyArrayFirst();
        }

        return buffer[0];
    }

    Element* last() {
        if (size == 0) {
            emptyArrayLast();
        }

        return buffer[size - 1];
    }

    Element[?]* data() {
        return buffer;
    }

    mutating mutable Element[?]* data() {
        return buffer;
    }

    /// Adds the given element to the end of the array.
    mutating void push(Element newElement) {
        if (size == capacity) {
            grow();
        }

        // TODO: Shouldn't need '&' here.
        (&buffer[size]).init(newElement);
        size++;
    }

    /// Ensures that the capacity is large enough to store the given number of elements.
    mutating void reserve(int minimumCapacity) {
        if (minimumCapacity > capacity) {
            var newBuffer = allocateArray<Element>(size: minimumCapacity);

            for (var index in 0..size) {
                var source = &buffer[index];
                var target = &newBuffer[index];
                target.init(*source);
            }

            if (capacity != 0) {
                deallocate(buffer);
            }

            buffer = newBuffer;
            capacity = minimumCapacity;
        }
    }

    /// Removes the first element from the array.
    /// Other elements are moved towards the beginning of the array by one index.
    mutating void removeFirst() {
        if (size == 0) {
            emptyArrayRemoveFirst();
        }

        unsafeRemoveAt(0);
    }

    /// Removes the last element from the array.
    mutating void removeLast() {
        if (size == 0) {
            emptyArrayRemoveLast();
        }

        size--;
        buffer[size].deinit();
    }

    /// Removes and returns the last element.
    mutating Element pop() {
        if (size == 0) {
            emptyArrayRemoveLast();
        }

        size--;
        return buffer[size];
    }

    /// Removes the element at the given index from the array.
    /// Elements following the removed element are moved towards the beginning of the array by one index.
    mutating void removeAt(int index) {
        if (index >= size) {
            indexOutOfBounds(index);
        }

        unsafeRemoveAt(index);
    }

    mutating private void unsafeRemoveAt(int index) {
        buffer[index].deinit();

        for (var i in (index + 1)..size) {
            var source = &buffer[i];
            var target = &buffer[i - 1];
            target.init(*source);
        }

        size--;
    }

    ArrayIterator<Element> iterator() {
        // TODO: When implicit initializers have been implemented, remove the ArrayRef() call below.
        return ArrayIterator(ArrayRef(this));
    }

    mutating MutableArrayIterator<Element> iterator() {
        return MutableArrayIterator(this);
    }

    EnumeratedIterator<Element> enumerate() {
        return EnumeratedIterator(iterator());
    }

    Array<Output> map<Input, Output>(Output(Input) transform) {
        var output = Array<Output>(capacity: this.size);

        for (var element in this) {
            output.push(transform(element));
        }

        return output;
    }

    Array<Element> filter<Input>(bool(Input) include) {
        var output = Array<Element>();

        for (var element in this) {
            if (include(element)) {
                output.push(*element);
            }
        }

        return output;
    }

    mutating private void grow() {
        if (capacity == 0) {
            reserve(1);
        } else {
            reserve(capacity * 2);
        }
    }

    private void indexOutOfBounds(int index) {
        printf("Array index %d is out of bounds, size is %d\n", index, size());
        fatalError();
    }

    private void emptyArrayFirst() {
        printf("Called first() on empty Array\n");
        fatalError();
    }

    private void emptyArrayLast() {
        printf("Called last() on empty Array\n");
        fatalError();
    }

    private void emptyArrayRemoveFirst() {
        printf("Called removeFirst() on empty Array\n");
        fatalError();
    }

    private void emptyArrayRemoveLast() {
        printf("Called removeLast() on empty Array\n");
        fatalError();
    }
}

bool operator==<T>(ArrayRef<T> a, ArrayRef<T> b) {
    if (a.size() != b.size()) {
        return false;
    }

    for (var i in 0..a.size()) {
        if (*a[i] != *b[i]) {
            return false;
        }
    }

    return true;
}
