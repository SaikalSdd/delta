struct MapIterator<Key, Value>: Copyable, Iterator<(Key key, Value value)*> {
    ArrayIterator<List<(Key key, Value value)>> hashTableIterator;
    (Key key, Value value)[?]* current;

    init(Map<Key, Value>* map) {
        hashTableIterator = map.hashTable.iterator();
        current = undefined;
        skipEmptySlots();
    }

    bool hasValue() {
        return hashTableIterator.hasValue();
    }

    (Key key, Value value)* value() {
        return current;
    }

    void increment() {
        var slot = hashTableIterator.value();
        var slotEnd = &slot.data()[slot.size()];
        current++;

        // TODO: Allow comparing T[?]* and T* without casts.
        if (cast<void*>(current) === cast<void*>(slotEnd)) {
            hashTableIterator.increment();
            skipEmptySlots();
        }
    }

    private void skipEmptySlots() {
        while (hashTableIterator.hasValue()) {
            if (hashTableIterator.value().size() != 0) {
                current = hashTableIterator.value().data();
                break;
            }
            hashTableIterator.increment();
        }
    }
}
