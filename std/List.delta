struct List<Element> {
    Element[?]* buffer;
    int size;
    int capacity;

    /// Initializes an empty list.
    init() {
        buffer = undefined;
        size = 0;
        capacity = 0;
    }

    /// Initializes an empty list with pre-allocated capacity.
    init(int capacity) {
        init();
        reserve(capacity);
    }

    /// Initializes an list containing the elements of the given array.
    init(Element[]* elements) {
        init(capacity: elements.size());

        // TODO: Change this to a for-each loop when arrays can be iterated.
        for (var index in 0..elements.size()) {
            push(elements[index]);
        }
    }

    deinit() {
        if (capacity != 0) {
            // FIXME: Call deinitializers of remaining elements.
            deallocate(buffer);
        }
    }

    /// Returns the number of elements in the list.
    int size() {
        return size;
    }

    /// Returns true if the list has no elements, otherwise false
    bool empty() {
        return size == 0;
    }

    /// Returns the number of elements the list can store without allocating more memory.
    int capacity() {
        return capacity;
    }

    /// Returns the element at the given index.
    Element* operator[](int index) {
        if (index >= size) {
            indexOutOfBounds(index);
        }

        return buffer[index];
    }

    Element* first() {
        if (size == 0) {
            emptyListFirst();
        }

        return buffer[0];
    }

    Element* last() {
        if (size == 0) {
            emptyListLast();
        }

        return buffer[size - 1];
    }

    Element[?]* data() {
        return buffer;
    }

    /// Adds the given element to the end of the list.
    void push(Element newElement) {
        if (size == capacity) {
            grow();
        }

        // TODO: Shouldn't need '&' here.
        (&buffer[size]).init(newElement);
        size++;
    }

    /// Ensures that the capacity is large enough to store the given number of elements.
    void reserve(int minimumCapacity) {
        if (minimumCapacity > capacity) {
            var newBuffer = allocateArray<Element>(size: minimumCapacity);

            for (var index in 0..size) {
                var source = &buffer[index];
                var target = &newBuffer[index];
                target.init(*source);
            }

            if (capacity != 0) {
                deallocate(buffer);
            }

            buffer = newBuffer;
            capacity = minimumCapacity;
        }
    }

    /// Removes the first element from the list.
    /// Other elements are moved towards the beginning of the list by one index.
    void removeFirst() {
        if (size == 0) {
            emptyListRemoveFirst();
        }

        unsafeRemoveAt(0);
    }

    /// Removes the last element from the list.
    void removeLast() {
        if (size == 0) {
            emptyListRemoveLast();
        }

        size--;
        buffer[size].deinit();
    }

    /// Removes and returns the last element.
    Element pop() {
        if (size == 0) {
            emptyListRemoveLast();
        }

        size--;
        return buffer[size];
    }

    /// Removes the element at the given index from the list.
    /// Elements following the removed element are moved towards the beginning of the list by one index.
    void removeAt(int index) {
        if (index >= size) {
            indexOutOfBounds(index);
        }

        unsafeRemoveAt(index);
    }

    private void unsafeRemoveAt(int index) {
        buffer[index].deinit();

        for (var i in (index + 1)..size) {
            var source = &buffer[i];
            var target = &buffer[i - 1];
            target.init(*source);
        }

        size--;
    }

    ArrayIterator<Element> iterator() {
        // TODO: When implicit initializers have been implemented, remove the ArrayRef() call below.
        return ArrayIterator(ArrayRef(this));
    }

    EnumeratedIterator<Element> enumerate() {
        return EnumeratedIterator(iterator());
    }

    List<Output> map<Input, Output>(Output(Input) transform) {
        var output = List<Output>(capacity: this.size);

        for (var element in this) {
            output.push(transform(element));
        }

        return output;
    }

    List<Element> filter<Input>(bool(Input) include) {
        var output = List<Element>();

        for (var element in this) {
            if (include(element)) {
                output.push(*element);
            }
        }

        return output;
    }

    private void grow() {
        if (capacity == 0) {
            reserve(1);
        } else {
            reserve(capacity * 2);
        }
    }

    private void indexOutOfBounds(int index) {
        printf("List index %d is out of bounds, size is %d\n", index, size());
        fatalError();
    }

    private void emptyListFirst() {
        printf("Called first() on empty List\n");
        fatalError();
    }

    private void emptyListLast() {
        printf("Called last() on empty List\n");
        fatalError();
    }

    private void emptyListRemoveFirst() {
        printf("Called removeFirst() on empty List\n");
        fatalError();
    }

    private void emptyListRemoveLast() {
        printf("Called removeLast() on empty List\n");
        fatalError();
    }
}
