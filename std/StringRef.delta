struct StringRef: Copyable, Comparable, Hashable, Printable {
    var characters: ArrayRef<char>;

    init(pointer: char*, length: int) {
        characters = ArrayRef<char>(pointer, length);
    }

    /// Initializes a StringRef referring to a null-terminated C-style string.
    ///
    /// __Note:__ This initializer has a time complexity of O(n) because it has to calculate the
    /// length of the argument string. If you know the length beforehand, consider using
    /// `StringRef.init(pointer: char*, length: uint)` instead.
    ///
    init(cString: char*) {
        characters = ArrayRef<char>(cString, int(strlen(cString)));
    }

    init (string: String*) {
        characters = ArrayRef<char>(string.data(), string.size());
    }

    def size(): int {
        return characters.size();
    }

    /// Returns the character at the given index.
    def [](index: int): char {
        return *characters[index];
    }

    def data(): char[?]* {
        return characters.data();
    }

    def contains(c: char): bool {
        return find(c, 0) != -1;
    }

    /// Returns the index of the given character, or -1 if it's not found
    def find(c: char): int {
        return find(c, 0);
    }

    /// Returns the index of the given character, or -1 if it's not found.
    /// Starts from start
    def find(c: char, start: int): int {
        for (var i in start..size()) {
            if (*characters[i] == c) {
                return int(i);
            }
        }
        return -1;
    }

    /// Supports using strings with sets and dicts
    def hash(): uint64 {
        var hashValue: uint64 = 5381;

        for (var index in 0..characters.size()) {
            hashValue = ((hashValue << 5) + hashValue) + uint64(this[index]);
        }

        return hashValue;
    }

    def iterator(): StringIterator {
        return StringIterator(this);
    }

    def printTo(stream: mutable String*) {
        stream.write(this);
    }

    def compare(other: StringRef*): Ordering {
        for (var index in 0..*min(size(), other.size())) {
            if (this[index] < other[index]) { return Ordering.Less; }
            if (this[index] > other[index]) { return Ordering.Greater; }
        }
        if (size() < other.size()) { return Ordering.Less; }
        if (size() > other.size()) { return Ordering.Greater; }
        return Ordering.Equal;
    }
}

def ==(a: StringRef, b: StringRef): bool {
    if (a.size() != b.size()) {
        return false;
    }

    for (var index in 0..a.size()) {
        if (a[index] != b[index]) {
            return false;
        }
    }

    return true;
}
